buildscript {
	ext {
		springBootVersion = '1.3.5.RELEASE'
	}
	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.0.1"
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'
apply plugin: 'org.sonarqube'
apply plugin: 'jacoco'

jar {
	baseName = 'admin-server'
	version = '0.0.1-SNAPSHOT'
}

springBoot {
	executable = true
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
}

dependencies {
	compile ('de.codecentric:spring-boot-admin-server:1.3.3'){
        exclude group : 'org.springframework.boot', module : 'spring-boot-starter-logging'
    }
	compile ('de.codecentric:spring-boot-admin-server-ui:1.3.3'){
        exclude group : 'org.springframework.boot', module : 'spring-boot-starter-logging'
    }
	compile ('org.springframework.cloud:spring-cloud-starter-eureka'){
		exclude group : 'org.springframework.boot', module : 'spring-boot-starter-logging'
	}
	compile('org.springframework.boot:spring-boot-starter-log4j2')
	testCompile('org.springframework.boot:spring-boot-starter-test') 
}

task copyJar(type: Copy) {
    dependsOn   'jar'
    from        "build/libs/${project.name}-${jar.version}.jar"
    into        'build/libs'
    rename { String fileName ->
        fileName.replace("-${jar.version}", "")
    }
}

processResources {
    filesMatching('**/*.properties'){
        expand(project.properties)
    }
}

jacoco {
    toolVersion = "0.7.4.201502262128"
}

jacocoTestReport {
    reports {
        xml {
            enabled true // coveralls plugin depends on xml format report
        }

        html {
            enabled true
        }
    }
}

build.dependsOn copyJar

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-starter-parent:Brixton.SR1"
		mavenBom "org.springframework.boot:spring-boot-starter-parent:1.3.5.RELEASE"
	}
}

eclipse {
	classpath {
		 containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
		 containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
	}
}
